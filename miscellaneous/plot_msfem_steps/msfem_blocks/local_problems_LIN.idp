// local_problems_LIN.idp
//
// //         //          ////////  ////////  //         //
// ////      ///  //////  //        //        ////      ///
// // //    ////  //      //////    //////    // //    ////
// //  //  // //  //////  //        //        //  //  // //
// //   ////  //      //  //        //        //   ////  //
// //    //   //  //////  //        ////////  //    //   //
// Computation of numerical correctors and bubble functions on a single coarse mesh element for MsFEM-LIN methods
//
//
// Scripts loaded ////////////////////////////////////////////////////////
// - offline_save_basis.idp 
//   -- save numerical correctors and bubble function
//
//
// Variables not defined in this script /////////////////////////////////
// - debug -- (int) provide helpful debugging output
// - TH -- (square) the coarse mesh of the global domain
// - Th -- (square) the fine mesh of the global domain
// - phi -- (VH0) function on the coarse mesh that is used to partition the global domain
// - xb, yb -- (VH0) [x/y]-coordinate of the barycentres of the triangles of TH
// - useVc -- (int) indicates whether the correctors for constants are to be used
// - useB -- (int) indicates whether bubbles are to be used
// - plotBubbles -- (int) indicates whether the user wishes to visualize V[c/x/y] and B
// - aBasis -- (macro) bilinear form for the local problems
//  
//
// Global variables declared in this script ////////////////////////////
// - (mesh)    K -- fine mesh of the coarse mesh element indicated by phi
// - (fespace) VK -- P1 space on K
// - (VK)      Vc, Vx, Vy, B -- numerical correctors and bubble function on K
// - (VK)      uH[x/y]
////////////////////////////////////////////////////////////////////////


mesh K=trunc(Th,phi>1,split=1,label=1); //fine mesh of the coarse mesh element K indicated by phi
fespace VK(K,P1); //P1 finite element space on the triangle K

VK Vc=0, Vx=0, Vy=0, uH=0, B=0, uHx=x-xb[][i], uHy=y-yb[][i];
// V[c/x/y] -- the numerical corrector associated to constants/the [x/y]-direction
// B -- bubble function for the MsFEM space associated to RHS = 1
// uH[x/y] -- the centered coordinate function in the [x/y]-direction that appears in the definition of the effective coefficients
// uH -- serves as a dummy in the variational form pb to set the RHS for V[c/x/y] and B

varf pb(uh,vh)=int2d(K)(aBasis(uh,vh)) + int1d(K)(tgv*uh*vh) //variational formulation of local problems -- bilinear form
            -int2d(K)(aBasis(uH,vh)); //variational formulation of local problems -- linear form for the RHS of the numerical correctors
varf id(unused,vh) = int2d(K)(vh); //variational formulation of local problems -- linear form of the RHS for the bubble functions

// Definition and resolution of the linear systems for the fine-scale problems
matrix A=pb(VK,VK, solver=UMFPACK);
real[int] F(VK.ndof);

if (useVc) {
    uH[]=1;  F=pb(0,VK);  Vc[]=A^-1*F;
}
uH[]=uHx[];  F=pb(0,VK);  Vx[]=A^-1*F;
uH[]=uHy[];  F=pb(0,VK);  Vy[]=A^-1*F;
if (useB) {
    F=id(0,VK);     B[]=A^-1*F;
}

if (plotBubbles) {
    if (useVc) plot(TH, Vc, fill=1, value=1, wait=1, hsv=colorhsv, cmm="Bubble function for constants");
    plot(Vx, fill=1, value=1, wait=1, hsv=colorhsv, cmm="Bubble function for x-direction");
    plot(Vy, fill=1, value=1, wait=1, hsv=colorhsv, cmm="Bubble function for y-direction");
    if (useB) plot(B, fill=1, value=1, wait=1, hsv=colorhsv, cmm="Bubble function for the RHS");
    fespace VPone(TH,P1); //coarse global FE conforming P1 space
    // Plot of multiscale basis function for first pone basis function only
    VPone pone=0; pone[][1]=1;
    VK phieps = pone + dx(pone)*Vx + dy(pone)*Vy;
    VK poneK=pone;
    plot(poneK, fill=1, value=1, wait=1, hsv=colorhsv, cmm="Pone basis functions");
    plot(phieps, fill=1, value=1, wait=1, hsv=colorhsv, cmm="Multiscale basis function");
}

include "offline_save_basis.idp" //store (externally) numerical correctors, bubble functions on K