// local_problems_LIN.idp
//
// Computation of numerical correctors and bubble functions on a single coarse mesh element
// for (adv-)MsFEM-LIN
//
//
// This file INCLUDES:
//  offline_save_basis.idp to save numerical correctors and bubble functions for.. 
//  ..a single coarse mesh element
//
// This file USES:
//  Th -- the fine mesh of the global domain
//  phi -- a P0 function on the coarse mesh that is used to partition the global domain
//  xb -- a P0 function on the coarse mesh 
//        associates to each triangle the x-coordinate of its barycentre
//  yb -- a P0 function on the coarse mesh
//        associates to each triangle the y-coordinate of its barycentre
//  a -- the bilinear of the variational formulation of the PDE to be solved
//  nu -- the (scalar) diffusion coefficient of the PDE
//  b[x/y] -- the components of the advection field of the PDE
//  storeV[x/y] -- array to store numerical correctors associated..
//                 ..to the .-direction for each coarse mesh element
//  storeB -- array to store the bubble functions for each coarse mesh element
//  
// This file CREATES for later use in the MsFEM:
//  Vx -- a P1 function on the fine mesh
//        the nuemrical corrector for the x direction associated to the coarse mesh element indicated by phi
//  Vy -- a P1 function on the fine mesh
//        the nuemrical corrector for the y direction associated to the coarse mesh element indicated by phi
//  B -- a P1 function on the fine mesh
//       the bubble function for the MsFEM space associated to RHS = 1 


mesh K=trunc(Th,phi>1,split=1,label=1); //fine mesh of the coarse mesh element K indicated by phi
fespace VK(K,P1); //P1 finite element space on the triangle K

VK Vx=0, Vy=0, uH=0, B=0, uHx=x-xb[][i], uHy=y-yb[][i];
// V[x/y] -- the numerical corrector associated to the [x/y]-direction
// B -- bubble function for the MsFEM space associated to RHS = 1
// uH[x/y] -- the coordinate function in the [x/y]-direction that appears in the definition of the effective coefficients
//  it can also be used for the definition of the numerical correctors and bubble functions since the barycentre vanishes under the gradient
// uH -- serves as a dummy in the variational form pb to set the RH for V[x/y] and B

varf pb(uh,vh)=int2d(K)(a(uh,vh,nu,bx,by)) + int1d(K)(tgv*uh*vh) //variational formulation of local problems -- bilinear form
            -int2d(K)(a(uH,vh,nu,bx,by)); //variational formulation of local problems -- linear form for the RHS of the numerical correctors
varf id(unused,vh) = int2d(K)(vh); //variational formulation of local problems -- linear form of the RHS for the bubble functions

// Definition and resolution of the linear systems for the fine-scale problems
matrix A=pb(VK,VK, solver=UMFPACK);
real[int] F(VK.ndof);

uH[]=uHx[];  F=pb(0,VK);  Vx[]=A^-1*F;
uH[]=uHy[];  F=pb(0,VK);  Vy[]=A^-1*F;
if (useB) {
    uH[]=1;     F=id(0,VK);     B[]=A^-1*F;
}

if (plotBubbles) {
    plot(TH, Vx, fill=1, value=1, wait=1, cmm="Bubble function for x-direction");
    plot(TH, Vy, fill=1, value=1, wait=1, cmm="Bubble function for y-direction");
    if (useB) plot(TH, B, fill=1, value=1, wait=1, cmm="Bubble function for the RHS");
}

include "offline_save_basis.idp" //store (externally) numerical correctors, bubble functions on K